<!-- AS -->
<!-- #INCLUDE FILE="../funzioni.asp" -->
<%
	
	'  -- GAM Lab s.r.l. --
	'  ProprietÃ  intellettuale di Ambra Gherardini - Gianni Gasperotti - Maurizio Sordo
	'  Copyright 2016 
	
	
	wck "origine","/s2000/assist_int/distbon_ass_lista.asp"
	
	'Se utente abilitato alla scelta aziende con la tendina passa sull'altro programma
	if elencoazi <> "N" then response.redirect "distbon_ass_azi.asp?dummy="+cstr(now())
	
	dim vet_fondi(500,5)
	
	azienda = gvarr ("azienda") 
	fondo = gvarr ("fondo") 
	anno = cvn(gvarr ("anno"))
	mese = cvn(gvarr ("mese")) 
	ordina = gvarr ("ordina") 
	estrai = gvarr ("estrai") 
	stampa= gvarr("stampa") 
	tabulato= gvarr("tabulato") 
	fuoco = gvarr("fuoco")
	
	if azienda = "" then 	azienda = rck("ultazi")
	
	'Se si sceglie una sola azienda passa sull'altro programma
	if azienda <> "" and instr(azienda,"*") = 0 then	response.redirect "distbon_ass_azi.asp?azienda="+azienda+"&amp;fondo="+fondo+"&amp;anno="+anno+"&amp;mese="+mese+"&amp;tabulato="+tabulato+"&amp;dummy="+cstr(now())
	
	if anno = "0" then anno = cvn(rck("anno"))
	if anno = "0" then anno = cvn(annoelab)
	if ccint(anno) > ccint(annoelab) then anno = cvn(annoelab)
	if ccint(mese) > 12 then mese = "12"
	if mese = "0" then mese = cvn(rck("mese"))
	if mese = "0" then 
	mese = cvn(month(date) -1)
	if mese = "0" then mese = "12"
	end if
	if ordina = "" then ordina = "rag"
	
	'Memorizzo i dati relativi ai fondi di assistenza complementare movimentati nell'anno
	if azienda <> "" then
	i = 0
	max_det = 0
	sql = "SELECT ana_azi, ana_fdo_assint, aaz_rag_soc, fan_vers_mesi, fan_cod_f24, fan_vers_tipo, fan_descr, fan_iban, fan_cin, fan_abi, fan_cab, fan_ccorr "
	sql = sql + " FROM storana "
	sql = sql + "   INNER JOIN fondiana ON fan_cod_fondo = ana_fdo_assint AND fan_tipo_fondo = 'A' "
	sql = sql + "   INNER JOIN anaazi ON aaz_azi = ana_azi "
	sql = sql + " WHERE ana_azi like '"+replace(cvs(azienda),"*","%")+"' " 
	sql = sql + " AND ana_anno = " + anno + " AND ana_mese <= " + mese
	if fondo <> "T" then sql = sql + " AND ana_fdo_assint = '" + fondo + "' "
	sql = sql + " GROUP BY ana_azi, ana_fdo_assint, aaz_rag_soc, fan_vers_mesi, fan_cod_f24, fan_vers_tipo, fan_descr, fan_iban, fan_cin, fan_abi, fan_cab, fan_ccorr "
	if ordina = "cod" then sql = sql + " ORDER BY ana_azi, fan_descr, ana_fdo_assint "
	if ordina = "rag" then sql = sql + " ORDER BY aaz_rag_soc, ana_azi, fan_descr, ana_fdo_assint "
	if ordina = "fon" then sql = sql + " ORDER BY fan_descr, ana_fdo_assint, aaz_rag_soc "
	set ars = sy_execute(sql)
	do until ars.eof
	i = i + 1
	vet_fondi(i,0) = ars("ana_azi")
	vet_fondi(i,1) = ars("ana_fdo_assint")
	vet_fondi(i,2) = trim(ars("fan_vers_mesi"))
	if trim(ars("fan_vers_tipo")) = "" then 
	vet_fondi(i,3) = ""
	if tabulato = "B" then vet_fondi(i,3) = "<span style='color:red;'>MANCA</span>"
	end if	
	if trim(ars("fan_vers_tipo")) = "F" then vet_fondi(i,3) = "<span style='color:green;'>F24 tributo '" & trim(ars("fan_cod_f24")) & "'</span>"
	if trim(ars("fan_vers_tipo")) = "B" then 
	vet_fondi(i,3) = trim(ars("fan_iban")) & " " & trim(ars("fan_cin")) & " " & frmn(cclng(ars("fan_abi")),5) & " " & frmn(cclng(ars("fan_cab")),5) & " " & trim(ars("fan_ccorr"))
	if len(vet_fondi(i,3)) <> 31 then vet_fondi(i,3) = "<span style='color:red;'>MANCA IBAN</span>"
	if tabulato = "D" then vet_fondi(i,3) = "Bonifico"
	end if
	vet_fondi(i,4) = trim(ars("fan_descr"))
	
	'Controllo che per l'azienda non esista un periodo di versamento differente dallo standard
	sql = "SELECT tab2.aaa_valore FROM anaziagg AS tab1 "
	sql = sql + "   INNER JOIN anaziagg AS tab2 ON tab1.aaa_azi = tab2.aaa_azi AND tab1.aaa_anno = tab2.aaa_anno AND "
	sql = sql + "	tab1.aaa_argomento = tab2.aaa_argomento AND tab1.aaa_progr = tab2.aaa_progr "
	sql = sql + " WHERE  tab1.aaa_azi = '" + ars("ana_azi") + "' "
	sql = sql + " AND tab1.aaa_anno = 0 AND tab1.aaa_argomento = 'ASSINT' "
	sql = sql + " AND tab1.aaa_sottoarg = '01' AND tab1.aaa_valore = '" + ars("ana_fdo_assint") + "' "
	sql = sql + " AND tab2.aaa_sottoarg = '04' "
	set elers = sy_execute(sql)
	if not elers.eof then vet_fondi(i,2) = trim(elers("tab2.aaa_valore"))
	elers.close	
	
	ars.movenext
	loop		
	ars.close
	max_det = i
	end if
	
	
	tagtitle= "Distinta contribuzioni"
	if fuoco <> "" then
	onloadingg= "onLoad='document.fondi_assint." & fuoco & ".focus(); document.fondi_assint." & fuoco &".select()'"
	else
	if elencoazi<>"N" then
	onloadingg = "onLoad='document.fondi_assint.azienda.focus();'"
	else 
	onloadingg = "onLoad='document.fondi_assint.dazienda.focus();'"
	end if
	end if
	
%>
<!-- #INCLUDE FILE="../header_int.asp" -->

<form method="POST" action="distbon_ass_lista.asp" name="fondi_assint">
	<input type="hidden" name="dummy" value="<%=now()%>">
	<input type="hidden" name="fuoco" id="fuocoid">
	<div class="container-fluid">	
		<div class="box box-color box-bordered darkblue" > 
			<div class="box-title">
				<h3>Distinta/bonifico contribuzioni ad Assistenza Integrativa</h3>
			</div> 
			<div class="box-content">
				<div class="col-sm-4">
					<div class="form-group">
						<%if elencoazi<>"N" then %>
						<label for="azienda" class="control-label">Codice azienda</label>
						<select name="azienda" size="1" class="form-control" onchange="document.fondi_assint.fuoco.value='anno';document.fondi_assint.submit();">
							<%	
								sql = "SELECT * FROM anaazi WHERE (aaz_azi IN ('"+ inabil+"') OR aaz_azi Like '" + lkabil +"') "
								if disabili <> "" then sql = sql + " AND aaz_azi NOT IN ('" + disabili + "') "
								sql = sql + " ORDER BY aaz_rag_soc ASC "
								set eln = sy_execute(sql) 
								do until eln.eof
							%>
							<option <%if instr(azienda, eln("aaz_azi")) then response.write "selected"%> value="<%=eln("aaz_azi")%>" class="form-control">
							<%=eln("aaz_azi")%> - <%=eln("aaz_rag_soc")%></option>
							<%		eln.movenext
								loop 
							eln.close%></select> <%else%>
							<input type="hidden" id="azienda" name="azienda" size="20" value="<%=azienda%>">
							<input type="text" id="dazienda" name="dazienda" size="60" value="<%=azienda%><%if instr(azienda,"*")=0 then %><%=" "+ddazi(azienda)%><%end if%>" class="form-control"  onfocus="this.select()">
							<div id="h_azienda" class="boxsuggerimenti"></div>
							<script type="text/javascript">		
								new Ajax.Autocompleter("dazienda", "h_azienda", "../aziende.asp", {minChars: 1, frequency: 0.01,afterUpdateElement : xcerca});
								function xcerca(text, li){
									document.fondi_assint.azienda.value=li.id;
									document.fondi_assint.fuoco.value='anno';
									document.fondi_assint.submit()
								}
							</script>          
							<%end if%> 
					</div>
				</div>
				<div class="col-sm-1">
					<div class="form-group">
						<label for="Anno" class="control-label">Anno</label>
						<input type="text" name="anno" size="4" value="<%=anno%>" class="form-control" maxlength="4" onchange="document.fondi_assint.fuoco.value='mese';document.fondi_assint.submit();">
					</div>
				</div>
				<div class="col-sm-2">
					<div class="form-group">
						<label for="mese" class="control-label">Mese</label>
						<input type="text" name="mese" size="2" value="<%=mese%>" class="form-control" maxlength="2" onchange="document.fondi_assint.fuoco.value='tabulato';document.fondi_assint.submit();">
					</div>
				</div>
				<div class="col-sm-2">
					<div class="form-group">
						<label for="modulo" class="control-label">Modulo</label>
						<select name="tabulato" size="1" class="form-control" onchange="document.fondi_assint.fuoco.value='fondo';document.fondi_assint.submit();">
							<option <%if tabulato= "D" then response.write "selected"%> value="D">Distinta</option>				 
							<option <%if tabulato= "B" then response.write "selected"%> value="B">Bonifico</option>
						</select>
					</div>
				</div>
				<div class="col-sm-2">
					<div class="form-group">
						<label for="Anno" class="control-label">Fondo assistenziale</label>
						<select name="fondo" class="form-control">
													<option <%if trim(fondo) = "T" then response.write "selected" %>  value="T" class="form-control">TUTTI</option>
													<%
														if azienda <> "" and anno <> "" and mese <> "0" then 
														sql= "SELECT ana_fdo_assint, fan_descr FROM storana  "
														sql = sql + " INNER JOIN fondiana ON ana_fdo_assint = fan_cod_fondo "
														sql = sql + " WHERE fan_tipo_fondo = 'A'" 
														sql = sql + " AND ana_azi like '"+replace(cvs(azienda),"*","%")+"' "
														sql = sql + " AND ana_fdo_assint <> '' "
														sql = sql + " AND ana_anno=" + cvn(anno)
														sql = sql + " AND ana_mese <=" + cvn(mese) 
														sql = sql + " GROUP BY ana_fdo_assint, fan_descr "
														sql = sql + " ORDER BY fan_descr "
														set ars = sy_execute(sql) 
														do until ars.eof 
													%>        
													<option <%if trim(fondo) = trim(ars("ana_fdo_assint")) then response.write "selected" %>  value="<%=ars("ana_fdo_assint")%>" class="form-control"><%=ars("ana_fdo_assint")%>-<%=ars("fan_descr")%></option>
													<%	ars.movenext
														loop
														ars.close
													end if %>      
												</select>
					</div>
				</div>
				<div class="col-sm-1">
					<div class="form-action">
						<label for="btn" class="control-label">Azione</label>
						<input type="submit" value="estrai" class="btn btn-primary form-control" name="estrai"></td>
					</div>
				</div>
						
							
							<div class="messaggio_rosso">ATTENZIONE: si espongono i soli dettagli da versare nel anno/mese selezionato</div>
							
							
							<% if azienda <> "" and anno <> "0" and mese <> "0" and estrai <> "" then %>
							<br>
							<div class="risultati">
								<table style="width:85%;">
									<tr class="stitolo">
										<td rowspan="2">Codice&nbsp;<a href="distbon_ass_lista.asp?azienda=<%=azienda%>&anno=<%=anno%>&mese=<%=mese%>&fondo=<%=fondo%>&ordina=cod&estrai=estrai&dummy=<%=now()%>">
										<img src="../immagini/ordina.gif" alt="Ordinamento per codice" title="Ordinamento per codice"></a></td>
										<td rowspan="2">Azienda&nbsp;<a href="distbon_ass_lista.asp?azienda=<%=azienda%>&anno=<%=anno%>&mese=<%=mese%>&fondo=<%=fondo%>&ordina=rag&estrai=estrai&dummy=<%=now()%>">
										<img src="../immagini/ordina.gif" alt="Ordinamento per ragione sociale" title="Ordinamento per ragione sociale"></a></td>
										<td rowspan="2">Fondo Assistenza&nbsp;<a href="distbon_ass_lista.asp?azienda=<%=azienda%>&anno=<%=anno%>&mese=<%=mese%>&fondo=<%=fondo%>&ordina=fon&estrai=estrai&dummy=<%=now()%>">
										<img src="../immagini/ordina.gif" alt="Ordinamento per fondo" title="Ordinamento per fondo"></a></td>
										<td colspan="2">Versamento</td>
										<td>Trattenuta</td>
										<td>Contributo</td>
										<td rowspan="2">Totale<br>Versato</td>
									</tr>
									<tr class="stitolo">
										<td>Mesi</td>
										<%	if tabulato = "D" then %>  	
										<td>A mezzo</td>
										<%	else %>
										<td>Cod. IBAN / F24</td>
										<%	end if %>  	
										<td>Dipend.</td>
										<td>Azienda</td>
									</tr>
									<%	
										n_det = 0
										tratt_dip = 0
										contr_azi = 0
										contr_ass = 0
										qta_tfr = 0
										tratt_vol = 0
										iscr_dip = 0
										iscr_azi = 0
										tot_vers = 0
										
										for i = 1 to max_det
										if mid(vet_fondi(i,2),mese,1) = "S" then 
										mese_ini = mese
										for ix = mese-1 to 1 step -1
										if mid(vet_fondi(i,2),ix,1) = "N" then
										mese_ini = ix
										else
										ix = 1
										end if		  
										next
										if ccint(mese_ini) = ccint(mese) then 
										des_periodo = ddmese(mese)
										else
										des_periodo = ucase(left(ddmese(mese_ini),3)) & "-" & ucase(left(ddmese(mese),3))	
										end if 
										
										sql = "SELECT ana_fdo_assint,"
										sql = sql + " sum(ctr_ctrdip) AS tratt_dip, "
										sql = sql + " sum(ctr_ctrazi) AS contr_azi, "
										sql = sql + " count(ctr_impdip) AS num_dip "
										sql = sql + " FROM storctr "
										sql = sql + " 	INNER JOIN storana ON ctr_azi = ana_azi AND ctr_ndip = ana_ndip AND ctr_ndip99 = ana_ndip99 AND ctr_anno = ana_anno AND ctr_mese = ana_mese AND ctr_livel = ana_livel "
										sql = sql + " WHERE "
										sql = sql + " ctr_azi = '" + vet_fondi(i,0) + "' "
										sql = sql + " AND ana_fdo_assint = '" + vet_fondi(i,1) + "' "
										sql = sql + " AND (ctr_ente = '35' OR (ctr_ente = '03' AND ctr_sottoente = '01')) " 
										sql = sql + " AND ctr_anno=" + cvn(anno)
										sql = sql + " AND ctr_mese >=" + cvn(mese_ini) + " AND ctr_mese <= " + cvn(mese) 
										sql = sql + " GROUP BY ana_fdo_assint " 
										set frs = sy_execute(sql)
										do until frs.eof 
										tratt_dip = tratt_dip + ccdbl(frs("tratt_dip"))
										contr_azi = contr_azi + ccdbl(frs("contr_azi"))
										
										if c = "cellav" then 
										c = "cellaL"
										else 	
										c = "cellav"
										end if
										
										cod_azi = vet_fondi(i,0)
										des_azi = ddazi(vet_fondi(i,0)) 
										if vet_fondi(i,0) = old_azi then 
										cod_azi = ""
										des_azi = ""
										end if
									%>
									<tr class='<%=c%>' onmouseover="this.className='cellaO';" onmouseout="this.className='<%=c%>'">
										<td style="text-align:center">
											<a href="distbon_ass_azi.asp?azienda=<%=vet_fondi(i,0)%>&amp;azi_part=<%=azienda%>&amp;anno=<%=anno%>&amp;mese=<%=mese%>&amp;fondo=<%=fondo%>&amp;tabulato=<%=tabulato%>&amp;arrivo=lista&amp;estrai=estrai&amp;dummy=<%=now()%>">
											<%=cod_azi%></a></td>
											<td><%=des_azi%></td>
											<td><%=vet_fondi(i,4)%></td>
											<td><%=des_periodo%></td>
											<td><%=vet_fondi(i,3)%></td>
											<td style="text-align:right;"><%=formatnumber(ccdbl(frs("tratt_dip")),2)%></td>
											<td style="text-align:right;"><%=formatnumber(ccdbl(frs("contr_azi")),2)%></td>
											<td style="text-align:right;"><b><%=formatnumber(cdbl(frs("tratt_dip"))+ccdbl(frs("contr_azi")),2)%></b></td>
									</tr>
									
									<%					old_azi = vet_fondi(i,0)
										
										frs.movenext
										loop
										frs.close
										end if
										next	 
									%>
								</table>
							</div>
							<% end if %>  
							
							<br><div class="messaggio_rosso"><%=msg%></div>
							
							<% if cclng(i) = 0 and estrai <> "" then	%>
							<br><div class="messaggio">Nessun dato trovato per le selezioni impostatee</div>
							<% end if %>
							
					</form>
				<!-- #INCLUDE FILE="../footer.asp" -->				